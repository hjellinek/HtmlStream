(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "20-Nov-2024 14:54:00" {DSK}<Users>hjellinek>Projects>TrySVG>Lisp>HTMLSTREAMTEST.;17 12961  

      :CHANGES-TO (FUNCTIONS HTMLTEST.SHOW-TEXT-BOUNDS WINDOWTEST.SHOW-TEXT-BOUNDS)

      :PREVIOUS-DATE "19-Nov-2024 17:11:30" 
{DSK}<Users>hjellinek>Projects>TrySVG>Lisp>HTMLSTREAMTEST.;16)


(PRETTYCOMPRINT HTMLSTREAMTESTCOMS)

(RPAQQ HTMLSTREAMTESTCOMS ((FUNCTIONS HTMLTEST.ONE-PAGE-SIMPLE HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS
                                  HTMLTEST.SHOW-TEXT-BOUNDS HTMLTEST.ONE-PAGE-WITH-CURVE 
                                  HTMLTEST.TEXT-TWO-FONTS HTMLTEST.TEXT-VERTICAL-OVERFLOW 
                                  HTMLTEST.TEXT-HORIZONTAL-OVERFLOW)
                           (FUNCTIONS WINDOWTEST.SHOW-TEXT-BOUNDS)
                           (FUNCTIONS HTMLTEST.TRANSFORM HTMLTEST.TRANSFORM-POINTS)))

(CL:DEFUN HTMLTEST.ONE-PAGE-SIMPLE (&OPTIONAL (FILE-NAME "/tmp/one-page-simple.html"))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (CL:FORMAT HTMLSTREAM "First line, Y position = ~d" (DSPYPOSITION NIL HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Second line, Y position = ~d" (DSPYPOSITION NIL HTMLSTREAM))
          (MOVETO 40 50 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Third line: Moved to ~d,~d" (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS (&OPTIONAL (FILE-NAME 
                                                                 "/tmp/one-page-simple-graphics.html"
                                                                   ))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (CL:FORMAT HTMLSTREAM "First line, x = ~d, y = ~d!" (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Second line, x = ~d, y = ~d" (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (MOVETO 40 50 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Third line: Moved to ~d, ~d." (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (DRAWCIRCLE 140 150 130 NIL NIL HTMLSTREAM)
          (FILLCIRCLE 340 150 130 NIL HTMLSTREAM)
          (for ANGLE from 0 to 359 by 15 do (DRAWELLIPSE 200 250 100 180 ANGLE NIL NIL HTMLSTREAM))
          (for LINE-Y from 450 to 550 by 10 do (DRAWLINE 0 LINE-Y 800 LINE-Y (IQUOTIENT (- LINE-Y 449
                                                                                           )
                                                                                    10)
                                                      NIL HTMLSTREAM))
          (LET* ([POLY-POINTS '((100 . 100)
                                (50 . 125)
                                (150 . 175)
                                (200 . 100)
                                (150 . 50]
                 [STAR-POINTS '((50 . 0)
                                (21 . 90)
                                (98 . 35)
                                (2 . 35)
                                (79 . 90]
                 (TRANSFORMED-POLY-POINTS (HTMLTEST.TRANSFORM-POINTS POLY-POINTS -20 -20 3 3))
                 (TRANSFORMED-STAR-POINTS (HTMLTEST.TRANSFORM-POINTS STAR-POINTS 0 200 3 3)))
                (DRAWPOLYGON POLY-POINTS T '(ROUND 3)
                       '(4 2)
                       HTMLSTREAM)
                (DRAWPOLYGON TRANSFORMED-POLY-POINTS NIL '(ROUND 3)
                       '(4 2)
                       HTMLSTREAM)
                (FILLPOLYGON TRANSFORMED-STAR-POINTS NIL HTMLSTREAM)
                (LET [(CURVE-KNOTS '((10 . 10)
                                     (50 . 50)
                                     (100 . 10)
                                     (150 . 50]
                     (DRAWCURVE CURVE-KNOTS NIL NIL NIL HTMLSTREAM)))
          FILE-NAME))

(CL:DEFUN HTMLTEST.SHOW-TEXT-BOUNDS (&OPTIONAL (FILE-NAME "/tmp/text-bounds.html"))
   "Output text, draw bounds, dump widths"

   (* ;; "Remember: in SVG, Y = 0 is at the top left; Y increases as you go down the page.")

   (* ;; "We invert the Y coordinate to match Medley:")

   (* ;; "    In Medley, Y = 0 is the bottom left")

   (* ;; "Text is drawn with baseline at current Y position")

   (* ;; "The bounding box ends short of the width of the text. the metrics are too small.  WHY?")

   (CL:WITH-OPEN-STREAM [STREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Text Bounds"]
          (LET ((SAMPLE "All work and no play makes Jack a dull boy. 1234567890!")
                (FONT10 (FONTCREATE 'NOTO-SANS-DISPLAY 10 'REGULAR NIL STREAM))
                (FONT20 (FONTCREATE 'NOTO-SANS-DISPLAY 20 'REGULAR NIL STREAM))
                STARTING-X STARTING-Y BASELINE-Y FONT-HEIGHT SAMPLE-WIDTH)

               (* ;; "display a string with a corner of a box at its origin, the width of the text")

               (DSPFONT FONT10 STREAM)
               (TERPRI STREAM)
               (SETQ FONT-HEIGHT (FONTPROP (DSPFONT NIL STREAM)
                                        'HEIGHT))
               (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
               (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
               (PRINTOUT T "SVG y pos before text = " (- (DSPTOPMARGIN NIL STREAM)
                                                         (DSPYPOSITION NIL STREAM))
                      T)
               (PRINTOUT STREAM SAMPLE T)
               (SETQ SAMPLE-WIDTH (STRINGWIDTH SAMPLE (DSPFONT NIL STREAM)))
               (PRINTOUT T "SVG y pos before polygon = " (- (DSPTOPMARGIN NIL STREAM)
                                                            (DSPYPOSITION NIL STREAM))
                      T)
               (DRAWPOLYGON (LIST (CONS (+ STARTING-X SAMPLE-WIDTH)
                                        STARTING-Y)
                                  (CONS STARTING-X STARTING-Y)
                                  (CONS STARTING-X (+ STARTING-Y FONT-HEIGHT)))
                      NIL NIL NIL STREAM)
               (PRINTOUT T "SVG y pos after polygon = " (- (DSPTOPMARGIN NIL STREAM)
                                                           (DSPYPOSITION NIL STREAM))
                      T)
               (TERPRI STREAM)
               (TERPRI STREAM)
               (TERPRI STREAM)
               FILE-NAME)))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-CURVE (&OPTIONAL (FILE-NAME "/tmp/one-page-with-curve.html"))
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (DRAWCURVE '((10 . 10)
                       (50 . 50)
                       (100 . 10)
                       (150 . 50))
                 NIL
                 '(ROUND 5)
                 '(1 1 1 2)
                 HTMLSTREAM)
          FILE-NAME))

(CL:DEFUN HTMLTEST.TEXT-TWO-FONTS (&OPTIONAL (FILE-NAME "/tmp/two-fonts.html"))
   "Output text, change fonts, output more text"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "Two fonts"))
          (LET* ((FIRST-FONT (DSPFONT NIL HTMLSTREAM))
                 (SECOND-FONT (FONTCREATE 'NOTO-SANS-DISPLAY 20 'REGULAR NIL HTMLSTREAM))
                 (SECOND-FONT-BOLD (FONTCREATE 'NOTO-SANS-DISPLAY 20 '(BOLD REGULAR REGULAR)
                                          NIL HTMLSTREAM))
                 (SECOND-FONT-ITALIC (FONTCREATE 'NOTO-SANS-DISPLAY 20 '(MEDIUM ITALIC REGULAR)
                                            NIL HTMLSTREAM))
                 (SECOND-FONT-BOLD-ITALIC (FONTCREATE 'NOTO-SANS-DISPLAY 20 '(BOLD ITALIC REGULAR)
                                                 NIL HTMLSTREAM)))
                (CL:FORMAT HTMLSTREAM "This is in the default font, ~s.~%%" FIRST-FONT)
                (TERPRI HTMLSTREAM)
                (DSPFONT SECOND-FONT HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "We just changed the font to ~s.~%%" (DSPFONT NIL HTMLSTREAM))
                (TERPRI HTMLSTREAM)
                (DSPFONT SECOND-FONT-BOLD HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "That font but bold, ~s.~%%" (DSPFONT NIL HTMLSTREAM))
                (TERPRI HTMLSTREAM)
                (DSPFONT SECOND-FONT-ITALIC HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "That font but italic, ~s.~%%" (DSPFONT NIL HTMLSTREAM))
                (TERPRI HTMLSTREAM)
                (DSPFONT SECOND-FONT-BOLD-ITALIC HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "Bold and italic too, ~s.~%%" (DSPFONT NIL HTMLSTREAM))
                (TERPRI HTMLSTREAM)
                FILE-NAME)))

(CL:DEFUN HTMLTEST.TEXT-VERTICAL-OVERFLOW (&OPTIONAL (FILE-NAME "/tmp/vertical-overflow.html"))
   "Output enough text to force a second page"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (for LINE from 0 to 66 do (CL:FORMAT HTMLSTREAM "This is line ~d.  Y position = ~d" LINE
                                           (DSPYPOSITION NIL HTMLSTREAM))
                                    (TERPRI HTMLSTREAM))
          (CL:FORMAT HTMLSTREAM "This line will overflow.  Y position = ~d" (DSPYPOSITION NIL 
                                                                                   HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          FILE-NAME))

(CL:DEFUN HTMLTEST.TEXT-HORIZONTAL-OVERFLOW (&OPTIONAL (FILE-NAME "/tmp/horizontal-overflow.html"))
   "Output enough text to wrap around the right margin"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (CL:FORMAT HTMLSTREAM "char width = ~d~%%" (CHARWIDTH (CHARCODE SPACE)
                                                            HTMLSTREAM))
          (CL:FORMAT HTMLSTREAM "X pos = ~d~%%" (DSPXPOSITION NIL HTMLSTREAM))
          (CL:FORMAT HTMLSTREAM "Font = ~A~%%" (DSPFONT NIL HTMLSTREAM))
          (for REPETITION from 0 to 50 do (CL:FORMAT HTMLSTREAM 
                                       "#~d: All work and no play makes Jack a dull boy 1234567890! "
                                                 REPETITION))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "The end!~%%")
          FILE-NAME))

(CL:DEFUN WINDOWTEST.SHOW-TEXT-BOUNDS (&OPTIONAL REUSE-WINDOW)
   "Output text, draw bounds, dump widths"

   (* ;; "Remember: Y = 0 is at the bottom left; Y decreases as you go down the page.")

   (* ;; "Text is drawn with baseline at current Y position")

   (LET* ((WINDOW (OR REUSE-WINDOW (CREATEW (create REGION
                                                   LEFT _ 610
                                                   BOTTOM _ 10
                                                   WIDTH _ (HTML.INCHES-TO-POINTS 8.5)
                                                   HEIGHT _ (HTML.INCHES-TO-POINTS 11))
                                          "Text Bounds")))
          (STREAM (WINDOWPROP WINDOW 'DSP))
          (SAMPLE "All work and no play makes Jack a dull boy. 1234567890")
          (FONT10 (FONTCREATE 'HELVETICA 10 'REGULAR NIL STREAM))
          (FONT20 (FONTCREATE 'HELVETICA 18 'REGULAR NIL STREAM))
          STARTING-X STARTING-Y BASELINE-Y FONT-HEIGHT SAMPLE-WIDTH)
         (CLEARW WINDOW)

         (* ;; "display a string with a corner of a box at its origin, the width of the text")

         (DSPFONT FONT10 STREAM)
         (TERPRI STREAM)
         (SETQ FONT-HEIGHT (FONTPROP (DSPFONT NIL STREAM)
                                  'HEIGHT))
         (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
         (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
         (SETQ SAMPLE-WIDTH (STRINGWIDTH SAMPLE (DSPFONT NIL STREAM)))
         (CL:FORMAT STREAM SAMPLE)
         (DRAWPOLYGON (LIST (CONS (+ STARTING-X SAMPLE-WIDTH)
                                  STARTING-Y)
                            (CONS STARTING-X STARTING-Y)
                            (CONS STARTING-X (+ STARTING-Y FONT-HEIGHT)))
                NIL NIL NIL STREAM)
         (TERPRI STREAM)
         (TERPRI STREAM)
         (TERPRI STREAM)))

(CL:DEFUN HTMLTEST.TRANSFORM (PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Move and scale a point"
   (CONS (+ X-TRANSLATE (ITIMES (CAR PT)
                               X-SCALE))
         (+ Y-TRANSLATE (ITIMES (CDR PT)
                               Y-SCALE))))

(CL:DEFUN HTMLTEST.TRANSFORM-POINTS (POINTS X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Translate and scale a list of points"
   (for PT in POINTS collect (HTMLTEST.TRANSFORM PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (928 1581 (HTMLTEST.ONE-PAGE-SIMPLE 928 . 1581)) (1583 4290 (
HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS 1583 . 4290)) (4292 6749 (HTMLTEST.SHOW-TEXT-BOUNDS 4292 . 6749
)) (6751 7201 (HTMLTEST.ONE-PAGE-WITH-CURVE 6751 . 7201)) (7203 8931 (HTMLTEST.TEXT-TWO-FONTS 7203 . 
8931)) (8933 9657 (HTMLTEST.TEXT-VERTICAL-OVERFLOW 8933 . 9657)) (9659 10557 (
HTMLTEST.TEXT-HORIZONTAL-OVERFLOW 9659 . 10557)) (10559 12422 (WINDOWTEST.SHOW-TEXT-BOUNDS 10559 . 
12422)) (12424 12695 (HTMLTEST.TRANSFORM 12424 . 12695)) (12697 12938 (HTMLTEST.TRANSFORM-POINTS 12697
 . 12938)))))
STOP

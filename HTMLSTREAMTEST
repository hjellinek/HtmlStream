(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "30-May-2025 15:31:50" {HTMLSTREAM}HTMLSTREAMTEST.;44 55443  

      :CHANGES-TO (VARS HTMLSTREAMTESTCOMS)
                  (FUNCTIONS HTMLTEST.ONE-PAGE-WITH-ARC HTMLTEST.ONE-PAGE-WITH-ARCS XFORM)

      :PREVIOUS-DATE "23-May-2025 15:38:22" {HTMLSTREAM}HTMLSTREAMTEST.;43)


(PRETTYCOMPRINT HTMLSTREAMTESTCOMS)

(RPAQQ HTMLSTREAMTESTCOMS
       ((FUNCTIONS GEN-TEST-BITMAP)
        (FUNCTIONS HTMLTEST.BITBLT HTMLTEST.TALL-BITBLT HTMLTEST.BLTSHADE)
        (FUNCTIONS HTMLTEST.ONE-PAGE-SIMPLE HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS 
               HTMLTEST.ONE-PAGE-WITH-CURVE HTMLTEST.ONE-PAGE-DASHING HTMLTEST.ONE-PAGE-WITH-SPIRAL 
               HTMLTEST.TEXT-TWO-FONTS HTMLTEST.TEXT-VERTICAL-OVERFLOW 
               HTMLTEST.TEXT-HORIZONTAL-OVERFLOW HTMLTEST.SPECIAL-TEXT-ENTITIES HTMLTEST.BRIEF-TEXT 
               HTMLTEST.TABS-IN-TEXT HTMLTEST.BRUSHES)
        
        (* ;; "color")

        (FUNCTIONS HTMLTEST.ONE-PAGE-WITH-COLOR)
        
        (* ;; "charsets")

        (FUNCTIONS HTMLTEST.FUNNY-CHARS HTMLTEST.UNDEFINED-CHARS)
        
        (* ;; "metrics")

        (FUNCTIONS HTMLTEST.SHOW-TEXT-BOUNDS HTMLTEST.SHOW-CHAR-BOUNDS 
               HTMLTEST.SHOW-COLOR-CHAR-BOUNDS)
        
        (* ;; "fonts")

        (FUNCTIONS HTMLTEST.NOTO-SANS)
        
        (* ;; "URL and HTML encodings")

        (FUNCTIONS HTMLTEST.URL-ENCODE HTMLTEST.HTML-ENCODE)
        
        (* ;; "HCFILES for HTML")

        (FUNCTIONS PARSE-DIRECTORIES DOT-FILE-P MAKE-CHILD-DIRECTORY)
        (FUNCTIONS WITH-BACKTRACE HTML-FILES CONVERT-SOURCE-TO-HTML CONVERT-TEDIT-TO-HTML)
        
        (* ;; "misc")

        (FUNCTIONS WINDOWTEST.SHOW-TEXT-BOUNDS)
        (FUNCTIONS HTMLTEST.TRANSFORM HTMLTEST.TRANSFORM-POINTS)))

(CL:DEFUN GEN-TEST-BITMAP (W H)
   "Generate a test bitmap image"
   (LET* [(BITMAP (BITMAPCREATE W H))
          (STREAM (DSPCREATE BITMAP))
          (FONT-HEIGHT (FONTPROP STREAM 'HEIGHT]
         (FOR X FROM 0 TO W DO (BITMAPBIT BITMAP X 0 1)
                               (BITMAPBIT BITMAP X (- H 1)
                                      1))
         (FOR Y FROM 0 TO H DO (BITMAPBIT BITMAP 0 Y 1)
                               (BITMAPBIT BITMAP (- W 1)
                                      Y 1))
         (MOVETO 2 (- H FONT-HEIGHT)
                STREAM)
         (CL:FORMAT STREAM "~dx~d" W H)
         BITMAP))

(CL:DEFUN HTMLTEST.BITBLT (&OPTIONAL (FILE-NAME "{DSK}/tmp/bitblt.html"))
   "Test bitblt in a single-page document"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "BitBLT test document"]
          (LET* ((W 400)
                 (H 50)
                 (TEST-BITMAP (GEN-TEST-BITMAP W H)))
                (PRINTOUT T "y1 = " (DSPYPOSITION NIL HTMLSTREAM)
                       T)
                (TERPRI HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "Here's some text.")
                (PRINTOUT T "y2 = " (DSPYPOSITION NIL HTMLSTREAM)
                       T)
                (TERPRI HTMLSTREAM)
                (BITBLT TEST-BITMAP 0 0 HTMLSTREAM 100 200)
                (MOVETO 40 500 HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "Second line"))
          FILE-NAME))

(CL:DEFUN HTMLTEST.TALL-BITBLT (&OPTIONAL (FILE-NAME "{DSK}/tmp/bitblt.html"))
   "Test bitblt in a single-page document"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "BitBLT test document"]
          (LET* ((W 50)
                 (H 400)
                 (TEST-BITMAP (GEN-TEST-BITMAP W H))
                 (SCALE (DSPSCALE NIL HTMLSTREAM)))
                (PRINTOUT T "y1 = " (DSPYPOSITION NIL HTMLSTREAM)
                       T)
                (TERPRI HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "Here's some text.")
                (PRINTOUT T "y2 = " (DSPYPOSITION NIL HTMLSTREAM)
                       T)
                (TERPRI HTMLSTREAM)
                (BITBLT TEST-BITMAP 0 0 HTMLSTREAM (TIMES 100 SCALE)
                       (TIMES 200 SCALE))
                (MOVETO (TIMES 40 SCALE)
                       (TIMES 100 SCALE)
                       HTMLSTREAM)
                (CL:FORMAT HTMLSTREAM "Second line"))
          FILE-NAME))

(CL:DEFUN HTMLTEST.BLTSHADE (&OPTIONAL (FILE-NAME "{DSK}/tmp/bltshade.html"))
   "Test BLTSHADE in a single-page document"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "BLTSHADE test document"]
          [LET* ((PAGE-HEIGHT (HTML.INCHES-TO-CENTIPOINTS 11))
                 (PAGE-WIDTH (HTML.INCHES-TO-CENTIPOINTS 8.5))
                 (FONT-HEIGHT (FONTPROP (DSPFONT NIL HTMLSTREAM)
                                     'HEIGHT))
                 (NUM-SHADES 16)
                 (BLT-WIDTH PAGE-WIDTH)
                 (BLT-HEIGHT (IQUOTIENT PAGE-HEIGHT NUM-SHADES))
                 (Y (- PAGE-HEIGHT BLT-HEIGHT))
                 (SHADE-BITS 0))
                (FOR SHADE FROM 0 TO NUM-SHADES
                   DO (LET ((SHADE (MASK.1'S 0 SHADE)))
                           (DRAWPOLYGON (LIST (CONS 0 Y)
                                              (CONS BLT-WIDTH Y)
                                              (CONS BLT-WIDTH (+ Y BLT-HEIGHT))
                                              (CONS 0 (+ Y BLT-HEIGHT)))
                                  T
                                  (DSPSCALE NIL HTMLSTREAM)
                                  NIL HTMLSTREAM)
                           (BLTSHADE SHADE HTMLSTREAM 0 Y BLT-WIDTH BLT-HEIGHT)
                           (MOVETO 0 (+ Y (/ BLT-HEIGHT 2))
                                  HTMLSTREAM)
                           (CL:FORMAT HTMLSTREAM "Shade ~16,'0B" SHADE)
                           (CL:DECF Y BLT-HEIGHT]
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-SIMPLE (&OPTIONAL (FILE-NAME "{DSK}/tmp/one-page-simple.html"))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "One page test document"]
          (FOR I FROM 0 TO 4 DO (MOVETO (ITIMES I 5000)
                                       (ITIMES I 5000)
                                       HTMLSTREAM)
                                (CL:FORMAT HTMLSTREAM "Line ~d  X,Y = ~d,~d" I (DSPXPOSITION NIL 
                                                                                      HTMLSTREAM)
                                       (DSPYPOSITION NIL HTMLSTREAM)))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS (&OPTIONAL (FILE-NAME 
                                                            "{DSK}/tmp/one-page-simple-graphics.html"
                                                                   ))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "One page test document"]
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "First line, x = ~d, y = ~d!" (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Second line, x = ~d, y = ~d" (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (MOVETO (P-TO-CP 40)
                 (P-TO-CP 50)
                 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Third line: Moved to ~d, ~d." (DSPXPOSITION NIL HTMLSTREAM)
                 (DSPYPOSITION NIL HTMLSTREAM))
          (DRAWCIRCLE (P-TO-CP 140)
                 (P-TO-CP 150)
                 (P-TO-CP 130)
                 NIL NIL HTMLSTREAM)
          (FILLCIRCLE (P-TO-CP 340)
                 (P-TO-CP 150)
                 (P-TO-CP 130)
                 NIL HTMLSTREAM)
          (for ANGLE from 0 to 359 by 15 do (DRAWELLIPSE (P-TO-CP 200)
                                                   (P-TO-CP 250)
                                                   (P-TO-CP 100)
                                                   (P-TO-CP 180)
                                                   ANGLE NIL NIL HTMLSTREAM))
          (for LINE-Y from (P-TO-CP 450) to (P-TO-CP 550) by (P-TO-CP 10)
             do (DRAWLINE 0 LINE-Y (P-TO-CP 800)
                       LINE-Y
                       (IQUOTIENT (- LINE-Y (P-TO-CP 449))
                              10)
                       NIL HTMLSTREAM))
          (LET* [[POLY-POINTS '((100 . 100)
                                (50 . 125)
                                (150 . 175)
                                (200 . 100)
                                (150 . 50]
                 [STAR-POINTS '((50 . 0)
                                (21 . 90)
                                (98 . 35)
                                (2 . 35)
                                (79 . 90]
                 (TRANSFORMED-POLY-POINTS (HTMLTEST.TRANSFORM-POINTS POLY-POINTS (P-TO-CP -20)
                                                 (P-TO-CP -20)
                                                 (TIMES 3 *CENTIPOINTS-PER-POINT*)
                                                 (TIMES 3 *CENTIPOINTS-PER-POINT*)))
                 (TRANSFORMED-STAR-POINTS (HTMLTEST.TRANSFORM-POINTS STAR-POINTS 0 (P-TO-CP 200)
                                                 (TIMES 3 *CENTIPOINTS-PER-POINT*)
                                                 (TIMES 3 *CENTIPOINTS-PER-POINT*]
                (DRAWPOLYGON POLY-POINTS T `(ROUND 300)
                       '(400 200)
                       HTMLSTREAM)
                (DRAWPOLYGON TRANSFORMED-POLY-POINTS NIL `(ROUND 300)
                       '(400 200)
                       HTMLSTREAM)
                (FILLPOLYGON TRANSFORMED-STAR-POINTS NIL HTMLSTREAM)
                (LET [(CURVE-KNOTS '((1000 . 1000)
                                     (5000 . 5000)
                                     (10000 . 1000)
                                     (15000 . 5000]
                     (DRAWCURVE CURVE-KNOTS NIL NIL NIL HTMLSTREAM)))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-CURVE (&OPTIONAL (FILE-NAME "{DSK}/tmp/one-page-with-curve.html"))
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Curve test document"]
          (LET ([POINTS '((10 . 10)
                          (50 . 50)
                          (100 . 10)
                          (150 . 50]
                (Y (DSPYPOSITION NIL HTMLSTREAM)))
               (CL:FORMAT HTMLSTREAM "Drawing ~S~%%" POINTS)
               (DRAWCURVE (HTMLTEST.TRANSFORM-POINTS POINTS 0 (- Y (HTML.INCHES-TO-CENTIPOINTS 1))
                                 *CENTIPOINTS-PER-POINT* *CENTIPOINTS-PER-POINT*)
                      NIL
                      `(ROUND ,(FQUOTIENT *CENTIPOINTS-PER-POINT* 2))
                      '(1000 1000 1000 2000)
                      HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-DASHING (&OPTIONAL (FILE-NAME "{DSK}/tmp/dashing.html"))
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Dashing test document"]
          (LET ((DASHING '(1000 2000 1000 3000 1000 4000 1000))
                (Y (DSPYPOSITION NIL HTMLSTREAM)))
               (DRAWLINE 1000 1000 60000 60000 100 NIL HTMLSTREAM NIL DASHING)
               (DRAWCIRCLE 30000 30000 3000 100 DASHING HTMLSTREAM)
               (DRAWELLIPSE 40000 40000 3000 5500 45 250 DASHING HTMLSTREAM)
               (DRAWPOLYGON '((10000 . 10000)
                              (5000 . 12500)
                              (15000 . 17500)
                              (20000 . 10000)
                              (15000 . 5000))
                      T 300 DASHING HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-SPIRAL (&OPTIONAL (FILE-NAME "{DSK}/tmp/one-page-with-spiral.html"))
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Spiral test document"]
          (LET* [(Y (DSPYPOSITION NIL HTMLSTREAM))
                 (ANGLE-INCREMENT (FQUOTIENT CL:PI 6))
                 (RADIUS-INCREMENT 10)
                 (ANGLE 0)
                 (RADIUS 1)
                 (NUM-POINTS 35)
                 (POINTS (for I from 0 to (- NUM-POINTS 1)
                            collect (PROG1 (CONS (FTIMES RADIUS (COS ANGLE T))
                                                 (FTIMES RADIUS (SIN ANGLE T)))
                                        (CL:INCF ANGLE ANGLE-INCREMENT)
                                        (CL:INCF RADIUS RADIUS-INCREMENT))]
                (CL:FORMAT HTMLSTREAM "Drawing ~S points~%%" (LENGTH POINTS))
                (DRAWCURVE (HTMLTEST.TRANSFORM-POINTS POINTS (HTML.INCHES-TO-CENTIPOINTS 5)
                                  (- Y (HTML.INCHES-TO-CENTIPOINTS 3))
                                  *CENTIPOINTS-PER-POINT* *CENTIPOINTS-PER-POINT*)
                       NIL
                       `(ROUND ,(FQUOTIENT *CENTIPOINTS-PER-POINT* 2))
                       '(1000 1000 1000 2000)
                       HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.TEXT-TWO-FONTS (&OPTIONAL (FILE-NAME "{DSK}/tmp/two-fonts.html"))
   "Output text, change fonts, output more text"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Two fonts"]
          (CL:FLET [(FONT-SPEC NIL (FONTPROP (DSPFONT NIL HTMLSTREAM)
                                          'SPEC]
                 (LET* ((FIRST-FONT (FONTCREATE 'NOTO-SANS-DISPLAY 10 'REGULAR NIL HTMLSTREAM))
                        (SECOND-FONT (FONTCREATE 'NOTO-SANS 20 'REGULAR NIL HTMLSTREAM))
                        (SECOND-FONT-BOLD (FONTCREATE 'NOTO-SANS 20 '(BOLD REGULAR REGULAR)
                                                 NIL HTMLSTREAM))
                        (SECOND-FONT-ITALIC (FONTCREATE 'NOTO-SANS 20 '(MEDIUM ITALIC REGULAR)
                                                   NIL HTMLSTREAM))
                        (SECOND-FONT-BOLD-ITALIC (FONTCREATE 'NOTO-SANS 20 '(BOLD ITALIC REGULAR)
                                                        NIL HTMLSTREAM)))
                       (CL:FORMAT HTMLSTREAM "This is in the default font, ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       (DSPFONT FIRST-FONT HTMLSTREAM)
                       (CL:FORMAT HTMLSTREAM "This is in the first font, ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       (DSPFONT SECOND-FONT HTMLSTREAM)
                       (CL:FORMAT HTMLSTREAM "We just changed the font to ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       (DSPFONT SECOND-FONT-BOLD HTMLSTREAM)
                       (CL:FORMAT HTMLSTREAM "That font but bold, ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       (DSPFONT SECOND-FONT-ITALIC HTMLSTREAM)
                       (CL:FORMAT HTMLSTREAM "That font but italic, ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       (DSPFONT SECOND-FONT-BOLD-ITALIC HTMLSTREAM)
                       (CL:FORMAT HTMLSTREAM "Bold and italic too, ~s.~%%" (FONT-SPEC))
                       (TERPRI HTMLSTREAM)
                       FILE-NAME))))

(CL:DEFUN HTMLTEST.TEXT-VERTICAL-OVERFLOW (&OPTIONAL (FILE-NAME "{DSK}/tmp/vertical-overflow.html"))
   "Output enough text to force a second page"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Overflow test document"]
          (for LINE from 0 to 66 do (CL:FORMAT HTMLSTREAM "This is line ~d.  Y position = ~d" LINE
                                           (DSPYPOSITION NIL HTMLSTREAM))
                                    (TERPRI HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.TEXT-HORIZONTAL-OVERFLOW (&OPTIONAL (FILE-NAME 
                                                              "{DSK}/tmp/horizontal-overflow.html"))
   "Output enough text to wrap around the right margin"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE 
                                                                  "Horizontal overflow test document"
                                                                      ]
          (for REPETITION from 0 to 50 do (CL:FORMAT HTMLSTREAM 
                                       "#~d: All work and no play makes Jack a dull boy 1234567890! "
                                                 REPETITION))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "The end!~%%")
          FILE-NAME))

(CL:DEFUN HTMLTEST.SPECIAL-TEXT-ENTITIES (&OPTIONAL (FILE-NAME "{DSK}/tmp/special-entities.html"))
   "Output enough text to wrap around the right margin"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE 
                                                                     "Special entities test document"
                                                                      ]
          (for REPETITION from 0 to 50 do (CL:FORMAT HTMLSTREAM "#~d: All work & no play > All work & all play but < %"neither%"! These will pass through: &#39; &copy; "
                                                 REPETITION))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "The end!~%%")
          FILE-NAME))

(CL:DEFUN HTMLTEST.BRIEF-TEXT (&OPTIONAL (FILE-NAME "{DSK}/tmp/brief-text.html"))
   "Output brief text"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE 
                                                                  "Horizontal overflow test document"
                                                                      ]
          (CL:FORMAT HTMLSTREAM "Brief text no terpri")
          (CL:FORMAT HTMLSTREAM " more.")
          FILE-NAME))

(CL:DEFUN HTMLTEST.TABS-IN-TEXT (&OPTIONAL (FILE-NAME "{DSK}/tmp/tabs-in-text.html"))
   "Test text containing TAB characters."
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Tabs in text test document"]
          (LET ((NUM-LINES 30)
                (NUM-COLUMNS 12))
               (FOR I FROM 0 TO (- NUM-LINES 1)
                  DO [FOR COL FROM 1 TO NUM-COLUMNS DO (CL:FORMAT HTMLSTREAM (CONCAT "Col " COL))
                                                       (IF (NOT (= COL NUM-COLUMNS))
                                                           THEN (\OUTCHAR HTMLSTREAM (CHARCODE TAB]
                     (CL:FORMAT HTMLSTREAM "~%%")))
          FILE-NAME))

(CL:DEFUN HTMLTEST.BRUSHES (&OPTIONAL (FILE-NAME "{DSK}/tmp/brushes.html"))
   "Simple test of brushes"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Brush test document"]
          [LET* [(POLY-POINTS (HTMLTEST.TRANSFORM-POINTS '((100 . 100)
                                                           (50 . 125)
                                                           (150 . 175)
                                                           (200 . 100)
                                                           (150 . 50))
                                     0 0 100 100))
                 (STAR-POINTS '((50 . 0)
                                (21 . 90)
                                (98 . 35)
                                (2 . 35)
                                (79 . 90]
                (for BRUSH-SIZE in '(100 200 300 400 500)
                   do (for BRUSH in '(ROUND SQUARE) do (DRAWPOLYGON POLY-POINTS T (LIST BRUSH 
                                                                                        BRUSH-SIZE)
                                                              '(400 200)
                                                              HTMLSTREAM)
                                                       (SETQ POLY-POINTS (HTMLTEST.TRANSFORM-POINTS
                                                                          POLY-POINTS
                                                                          (P-TO-CP 20)
                                                                          (P-TO-CP 20)
                                                                          1 1]
          FILE-NAME))



(* ;; "color")


(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-COLOR (&OPTIONAL (BG-COLOR 'WHITE)
                                              (FILE-NAME "{DSK}/tmp/one-page-with-color.html"))
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Color test document"]
          (DSPBACKCOLOR BG-COLOR HTMLSTREAM)

          (* ;; "draw a green circle")

          (DSPCOLOR 'GREEN HTMLSTREAM)
          (DRAWCIRCLE (P-TO-CP 140)
                 (P-TO-CP 150)
                 (P-TO-CP 130)
                 NIL NIL HTMLSTREAM)

          (* ;; "now a red one")

          (DSPCOLOR 'RED HTMLSTREAM)
          (DRAWCIRCLE (P-TO-CP 150)
                 (P-TO-CP 160)
                 (P-TO-CP 140)
                 NIL NIL HTMLSTREAM)

          (* ;; "some RGB")

          (DSPCOLOR '(128 0 255)
                 HTMLSTREAM)
          (DRAWCIRCLE (P-TO-CP 160)
                 (P-TO-CP 170)
                 (P-TO-CP 140)
                 NIL NIL HTMLSTREAM)
          (MOVETO (P-TO-CP 10)
                 (P-TO-CP 170)
                 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "This is ~s text" '(128 0 255))
          (DRAWLINE (P-TO-CP 10)
                 (P-TO-CP 170)
                 (P-TO-CP 450)
                 (P-TO-CP 170)
                 NIL NIL HTMLSTREAM 'YELLOW)
          (DSPCOLOR 'BLUE HTMLSTREAM)
          (DRAWPOLYGON (LIST (CONS (P-TO-CP 20)
                                   (P-TO-CP 20))
                             (CONS (P-TO-CP 450)
                                   (P-TO-CP 20))
                             (CONS (P-TO-CP 450)
                                   (P-TO-CP 450)))
                 T NIL NIL HTMLSTREAM)
          (DSPCOLOR 'MAGENTA HTMLSTREAM)
          (DRAWELLIPSE (P-TO-CP 200)
                 (P-TO-CP 200)
                 (P-TO-CP 100)
                 (P-TO-CP 70)
                 0 NIL NIL HTMLSTREAM)
          (FILLCIRCLE (P-TO-CP 400)
                 (P-TO-CP 200)
                 (P-TO-CP 98)
                 NIL HTMLSTREAM)
          (DSPCOLOR '(128 128 128)
                 HTMLSTREAM)
          (FILLPOLYGON (LIST (CONS (P-TO-CP 430)
                                   (P-TO-CP 30))
                             (CONS (P-TO-CP 860)
                                   (P-TO-CP 30))
                             (CONS (P-TO-CP 860)
                                   (P-TO-CP 860)))
                 NIL HTMLSTREAM 0)
          FILE-NAME))



(* ;; "charsets")


(CL:DEFUN HTMLTEST.FUNNY-CHARS (&OPTIONAL (FILE-NAME "{DSK}/tmp/funny-chars.html"))
   "Create a document that displays characters from various non-zero charsets"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Valid charsets test document"]
          (DSPFONT (FONTCREATE 'HELVETICA 10 'MRR 0 'HTML)
                 HTMLSTREAM)
          (LET [(EXAMPLES '(("Charset 0 (Latin)" 0 ("Capital letters" 65 90)
                                   ("Small letters" 97 122))
                            ("Accented Latin 1" 241 ("Capital letters" 33 43)
                                   ("Small letters" 161 171))
                            ("Accented Latin 2" 242 ("Capital letters" 33 43)
                                   ("Small letters" 161 171))
                            ("Greek" 38 ("Capital letters" 65 93)
                                   ("Small letters" 97 125))
                            ("Accented Greek 1" 243 ("A sample" 33 43))
                            ("Accented Greek 2" 244 ("A sample" 33 43))
                            ("Cyrillic" 39 ("Capital letters" 33 65)
                                   ("Small letters" 81 113))
                            ("Runic & Gothic" 41 ("Runes" 33 44)
                                   ("Gothic" 225 230))
                            ("Extended Cyrillic" 42 ("A sample" 46 51))
                            ("Arabic" 224 ("Consonants and vowels" 66 106))
                            ("Extended Arabic" 228 ("Indian numerals" 48 57)
                                   ("Isolated Arabic chars" 74 84))
                            ("Initial, medial, and final Arabic characters" 245 ("A sample" 33 43))
                            ("Hebrew" 225 ("Consonants" 64 93))
                            ("Katakana" 37 ("A sample" 33 54))
                            ("Hiragana and Bo-po-mo-fo" 36 ("A sample" 33 54))
                            ("JIS Level-I Japanese kanji" 48 ("A sample" 33 54))
                            ("JIS Level-I Japanese kanji" 49 ("A sample" 33 54))
                            ("JIS Level-I Japanese kanji" 79 ("A sample" 33 54))
                            ("Misc Japanese Symbols" 116 ("A sample" 34 42))
                            ("Hangul" 227 ("A sample" 193 203))
                            ("Georgian" 228 ("A sample" 33 43))
                            ("Armenian" 228 ("A sample" 161 171))
                            ("Forms" 40 ("Box elements" 35 42)
                                   ("Mosaic tiles" 161 169))
                            ("IPA" 226 ("Schwa assortment" 95 98)
                                   ("Vowels" 81 90)
                                   ("Plosives and fricatives" 162 172))
                            ("Devanagari" 229 ("A sample" 165 174))
                            ("Variant representations for graphic characters" 253 ("A sample" 224 233
                                                                                         ))
                            ("General and technical symbols 3" 235 ("A sample" 33 43))
                            ("General and technical symbols 2" 238 ("A sample" 47 58))
                            ("General and technical symbols 1" 239 ("Random sample" 33 43)
                                   ("Roman numerals" 193 202)
                                   ("Funny" 222 224)
                                   ("Zodiac" 234 251))
                            ("ITC Dingbats 1" 237 ("A sample" 33 44))
                            ("ITC Dingbats 2 and general symbols" 236 ("Hands and scissors" 34 38)
                                   ("Other hands" 117 119)
                                   ("Circled numbers" 184 193))
                            ("Ligatures and field format symbols" 240 ("Fractions" 112 121)
                                   ("Control character names" 168 173)
                                   ("Movement and substitution" 184 199))
                            ("Enclosed numbers and letters" 118 ("A sample" 48 58))
                            ("JIS Level-II Japanese kanji 1" 80 ("A sample" 33 43))
                            ("JIS Level-II Japanese kanji 2" 81 ("A sample" 33 43]
               (CL:FLET [(SHOW-CHAR-RANGE (START END CHARSET)
                                (FOR BASE-CODE FROM START TO END
                                   DO (LET ((CH (LOGOR (LLSH CHARSET 8)
                                                       BASE-CODE)))
                                           (CL:FORMAT HTMLSTREAM "Code 0x~4,'0X = " CH)
                                           (\OUTCHAR HTMLSTREAM CH)
                                           (CL:FORMAT HTMLSTREAM "~%%"]
                      (CL:FORMAT HTMLSTREAM "Text in various character sets.~%%~%%")
                      (for EXAMPLE in EXAMPLES do (LET ((NAME (CAR EXAMPLE))
                                                        (CHARSET (CADR EXAMPLE)))
                                                       (CL:FORMAT HTMLSTREAM 
                                                      "This is #o~8,,'0,r ~:*0x~2,'0X ~:*~d (~A).~%%"
                                                              CHARSET NAME)
                                                       (for RANGE in (CDDR EXAMPLE)
                                                          do (CL:FORMAT HTMLSTREAM "~%%~A:~%%"
                                                                    (CAR RANGE))
                                                             (SHOW-CHAR-RANGE (CADR RANGE)
                                                                    (CADDR RANGE)
                                                                    CHARSET))
                                                       (DSPNEWPAGE HTMLSTREAM)))
                      (CL:FORMAT HTMLSTREAM "Done!~%%")
                      FILE-NAME))))

(CL:DEFUN HTMLTEST.UNDEFINED-CHARS (&OPTIONAL (FILE-NAME "{DSK}/tmp/undefined-chars.html"))
   "Create a document that tries to display undefined characters from various charsets, real or not"
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE 
                                                                   "Undefined charsets test document"
                                                                      ]
          (LET ((UNDEFINED-CHARSET 10)
                (START-GIBBERISH 65)
                (END-GIBBERISH 93))
               (CL:FLET [(SHOW-CHAR-RANGE (START END CHARSET)
                                (FOR BASE-CODE FROM START TO END
                                   DO (LET ((CH (LOGOR (LLSH CHARSET 8)
                                                       BASE-CODE)))
                                           (CL:FORMAT HTMLSTREAM "Code 0x~4,'0X = " CH)
                                           (\OUTCHAR HTMLSTREAM CH)
                                           (CL:FORMAT HTMLSTREAM "~%%"]
                      (CL:FORMAT HTMLSTREAM "Here's some text in charset 0.~%%")
                      (CL:FORMAT HTMLSTREAM "And now 0x~2,'0X (Undefined).~%%" UNDEFINED-CHARSET)
                      (CL:FORMAT HTMLSTREAM "Gibberish letters:~%%")
                      (SHOW-CHAR-RANGE START-GIBBERISH END-GIBBERISH UNDEFINED-CHARSET)

                      (* ;; "TODO output REPLACEMENT raw text: '(239 191 189)")

                      (CL:FORMAT HTMLSTREAM "Done!~%%")
                      FILE-NAME))))



(* ;; "metrics")


(CL:DEFUN HTMLTEST.SHOW-TEXT-BOUNDS (&OPTIONAL (FILE-NAME "{DSK}/tmp/text-bounds.html"))
   "Output text, draw bounds."

   (* ;; "Remember: in SVG, Y = 0 is at the top left; Y increases as you go down the page.")

   (* ;; "We invert the Y coordinate to match Medley:")

   (* ;; "    In Medley, Y = 0 is the bottom left")

   (* ;; "Text is drawn with baseline at current Y position")

   (CL:WITH-OPEN-STREAM [STREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Text Bounds"]
          (LET ((SAMPLE "All work and no play makes Jack a dull boy. 1234567890!")
                (FONT10 (FONTCREATE 'NOTO-SANS 10 'REGULAR NIL STREAM))
                (FONT12 (FONTCREATE 'NOTO-SANS 12 'REGULAR NIL STREAM))
                (FONT14 (FONTCREATE 'NOTO-SANS 14 'REGULAR NIL STREAM))
                STARTING-X STARTING-Y BASELINE-Y FONT-ASCENT FONT-DESCENT)
               (CL:FLET [(WRITE NIL (LET ((SAMPLE-WIDTH (STRINGWIDTH SAMPLE STREAM)))

                                         (* ;; 
                       "display a string with a corner of a box at its origin, the width of the text")

                                         (TERPRI STREAM)
                                         (SETQ FONT-ASCENT (FONTPROP STREAM 'ASCENT))
                                         (SETQ FONT-DESCENT (FONTPROP STREAM 'DESCENT))
                                         (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
                                         (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
                                         (CL:FORMAT STREAM "~a~%%" SAMPLE)

                                         (* ;; 
                      "draw from (end-X, baseline-Y) to (start-X, baseline-Y) to (start-X, height-Y)")

                                         (DRAWLINE (+ STARTING-X SAMPLE-WIDTH)
                                                STARTING-Y STARTING-X STARTING-Y 
                                                *CENTIPOINTS-PER-POINT* NIL STREAM)

                                         (* ;; 
                                         "draw from (start-X, descent-Y) to (start-X, ascent-Y)")

                                         (DRAWLINE STARTING-X (- STARTING-Y FONT-DESCENT)
                                                STARTING-X
                                                (+ STARTING-Y FONT-ASCENT)
                                                *CENTIPOINTS-PER-POINT* NIL STREAM)

                                         (* ;; 
                                         "draw a short line at the end, the height of the descent")

                                         (DRAWLINE (+ STARTING-X SAMPLE-WIDTH)
                                                (- STARTING-Y FONT-DESCENT)
                                                (+ STARTING-X SAMPLE-WIDTH)
                                                (+ STARTING-Y FONT-DESCENT)
                                                *CENTIPOINTS-PER-POINT* NIL STREAM)
                                         (TERPRI STREAM)
                                         (TERPRI STREAM)
                                         (TERPRI STREAM]
                      (DSPFONT FONT10 STREAM)
                      (WRITE)
                      (DSPFONT FONT12 STREAM)
                      (WRITE)
                      (DSPFONT FONT14 STREAM)
                      (WRITE))
               FILE-NAME)))

(CL:DEFUN HTMLTEST.SHOW-CHAR-BOUNDS (&OPTIONAL (FILE-NAME "{DSK}/tmp/char-bounds.html"))
   "Output text, draw bounds for each character."

   (* ;; "Remember: in SVG, Y = 0 is at the top left; Y increases as you go down the page.")

   (* ;; "We invert the Y coordinate to match Medley:")

   (* ;; "    In Medley, Y = 0 is the bottom left")

   (* ;; "Text is drawn with baseline at current Y position")

   (* ;; 
   "TODO The bounding box ends short of the width of the text. the metrics are too small.  WHY?")

   (CL:WITH-OPEN-STREAM [STREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Character Bounds"]
          (LET ((SAMPLE "All work and no play makes Jack a dull boy. 1234567890!")
                (FONT10 (FONTCREATE 'NOTO-SANS 10 'REGULAR NIL STREAM))
                (FONT14 (FONTCREATE 'NOTO-SANS 14 'REGULAR NIL STREAM))
                (LINEWIDTH (/ *CENTIPOINTS-PER-POINT* 2))
                STARTING-X STARTING-Y BASELINE-Y FONT-ASCENT FONT-DESCENT)
               (CL:FLET [(SAMPLE (FONT)
                                (DSPFONT FONT STREAM)
                                (LET ((SAMPLE-WIDTH (STRINGWIDTH SAMPLE STREAM)))
                                     (TERPRI STREAM)
                                     (SETQ FONT-ASCENT (FONTPROP STREAM 'ASCENT))
                                     (SETQ FONT-DESCENT (FONTPROP STREAM 'DESCENT))
                                     (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
                                     (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
                                     (CL:FORMAT STREAM "~a~%%" SAMPLE)
                                     (CL:FLET [(DRAWCHARBOX (CH X BASELINE)
                                                      (LET ((WIDTH (CHARWIDTH CH STREAM)))

                                                           (* ;; "draw the descent")

                                                           (DRAWLINE X (- BASELINE FONT-DESCENT)
                                                                  (+ X WIDTH)
                                                                  (- BASELINE FONT-DESCENT)
                                                                  LINEWIDTH NIL STREAM)

                                                           (* ;; "draw the baseline")

                                                           (DRAWLINE X BASELINE (+ X WIDTH)
                                                                  BASELINE LINEWIDTH NIL STREAM)

                                                           (* ;; "draw the left bearing")

                                                           (DRAWLINE X (- BASELINE FONT-DESCENT)
                                                                  X
                                                                  (+ BASELINE FONT-ASCENT)
                                                                  LINEWIDTH NIL STREAM)

                                                           (* ;; "draw the right bearing")

                                                           (DRAWLINE (+ X WIDTH)
                                                                  (- BASELINE FONT-DESCENT)
                                                                  (+ X WIDTH)
                                                                  (+ BASELINE FONT-ASCENT)
                                                                  LINEWIDTH NIL STREAM)

                                                           (* ;; "draw the ascent")

                                                           (DRAWLINE X (+ BASELINE FONT-ASCENT)
                                                                  (+ X WIDTH)
                                                                  (+ BASELINE FONT-ASCENT)
                                                                  LINEWIDTH NIL STREAM)
                                                           (+ X WIDTH]
                                            (for CH instring SAMPLE
                                               do (SETQ STARTING-X (DRAWCHARBOX CH STARTING-X 
                                                                          STARTING-Y)))
                                            (TERPRI STREAM)
                                            (TERPRI STREAM)
                                            (TERPRI STREAM]
                      (SAMPLE FONT10)
                      (SAMPLE FONT14))
               FILE-NAME)))

(CL:DEFUN HTMLTEST.SHOW-COLOR-CHAR-BOUNDS (&OPTIONAL (FILE-NAME "{DSK}/tmp/char-bounds.html"))
   "Output text, draw bounds for each character."

   (* ;; "Remember: in SVG, Y = 0 is at the top left; Y increases as you go down the page.")

   (* ;; "We invert the Y coordinate to match Medley:")

   (* ;; "    In Medley, Y = 0 is the bottom left")

   (* ;; "Text is drawn with baseline at current Y position")

   (* ;; 
   "TODO The bounding box ends short of the width of the text. the metrics are too small.  WHY?")

   (CL:WITH-OPEN-STREAM
    [STREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Character Bounds"]
    (LET ((SAMPLE "All work and no play makes Jack a dull boy. 1234567890!")
          (FONT10 (FONTCREATE 'NOTO-SANS 10 'REGULAR NIL STREAM))
          (FONT14 (FONTCREATE 'NOTO-SANS 14 'REGULAR NIL STREAM))
          (LINEWIDTH (/ *CENTIPOINTS-PER-POINT* 2))
          STARTING-X STARTING-Y BASELINE-Y FONT-ASCENT FONT-DESCENT)
         (CL:FLET [(SAMPLE (FONT)
                          (DSPFONT FONT STREAM)
                          (LET ((SAMPLE-WIDTH (STRINGWIDTH SAMPLE STREAM)))
                               (TERPRI STREAM)
                               (SETQ FONT-ASCENT (FONTPROP STREAM 'ASCENT))
                               (SETQ FONT-DESCENT (FONTPROP STREAM 'DESCENT))
                               (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
                               (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
                               (CL:FORMAT STREAM "~a~%%" SAMPLE)
                               (CL:FLET [(DRAWCHARBOX (CH X BASELINE)
                                                (LET ((ORIG-COLOR (DSPCOLOR NIL STREAM)))
                                                     (CL:UNWIND-PROTECT
                                                         (LET ((WIDTH (CHARWIDTH CH STREAM)))

                                                              (* ;; "draw red boxes")

                                                              (DSPCOLOR 'RED STREAM)

                                                              (* ;; "draw the descent")

                                                              (DRAWLINE X (- BASELINE FONT-DESCENT)
                                                                     (+ X WIDTH)
                                                                     (- BASELINE FONT-DESCENT)
                                                                     LINEWIDTH NIL STREAM)

                                                              (* ;; "draw the baseline")

                                                              (DRAWLINE X BASELINE (+ X WIDTH)
                                                                     BASELINE LINEWIDTH NIL STREAM)

                                                              (* ;; "draw the left bearing")

                                                              (DRAWLINE X (- BASELINE FONT-DESCENT)
                                                                     X
                                                                     (+ BASELINE FONT-ASCENT)
                                                                     LINEWIDTH NIL STREAM)

                                                              (* ;; "draw the right bearing")

                                                              (DRAWLINE (+ X WIDTH)
                                                                     (- BASELINE FONT-DESCENT)
                                                                     (+ X WIDTH)
                                                                     (+ BASELINE FONT-ASCENT)
                                                                     LINEWIDTH NIL STREAM)

                                                              (* ;; "draw the ascent")

                                                              (DRAWLINE X (+ BASELINE FONT-ASCENT)
                                                                     (+ X WIDTH)
                                                                     (+ BASELINE FONT-ASCENT)
                                                                     LINEWIDTH NIL STREAM)
                                                              (+ X WIDTH))
                                                         (DSPCOLOR ORIG-COLOR STREAM))]
                                      (for CH instring SAMPLE do (SETQ STARTING-X
                                                                  (DRAWCHARBOX CH STARTING-X 
                                                                         STARTING-Y)))
                                      (TERPRI STREAM)
                                      (TERPRI STREAM)
                                      (TERPRI STREAM]
                (SAMPLE FONT10)
                (SAMPLE FONT14))
         FILE-NAME)))



(* ;; "fonts")


(CL:DEFUN HTMLTEST.NOTO-SANS (&OPTIONAL (FILE-NAME "{DSK}/tmp/noto-sans.html"))
   "Write simple text to a file using the Noto Sans font, which seems broken somehow."
   (CL:WITH-OPEN-STREAM [HTMLSTREAM (OPENHTMLSTREAM FILE-NAME '(TITLE "Noto Sans test document"]

          (* ;; "The NOTO-SANS 12 font height is 14100.  NOTO-SANS-DISPLAY is 1500!")

          (LET ((FONT (FONTCREATE 'NOTO-SANS 12 'MRR 0 HTMLSTREAM)))
               (CL:FORMAT T "Font family: ~A~%%" (FONTPROP FONT 'FAMILY))
               (DSPFONT FONT HTMLSTREAM)
               (FOR LINE FROM 1 TO 6 DO (CL:FORMAT HTMLSTREAM 
                                    "~d. The quick brown fox jumped over the lazy dog 1234567890.~%%"
                                               LINE)))
          FILE-NAME))



(* ;; "URL and HTML encodings")


(CL:DEFUN HTMLTEST.URL-ENCODE ()
   "Test URL-ENCODE"
   (CL:FORMAT T "Testing html-encode:~%%")
   (CL:FORMAT T "%"Hello, world!%" -> ~a~%%" (URL-ENCODE "Hello, world!"))
   (CL:FORMAT T "%"1234567890%" -> ~a~%%" (URL-ENCODE "1234567890"))
   (CL:FORMAT T "%"Special chars: !@#$%%^&*()_+=-`~~[]{}|;':%",./<>?%" -> ~a~%%" (URL-ENCODE 
                                                   "Special chars: !@#$%%^&*()_+=-`~[]{}|;':%",./<>?"
                                                                                        ))
   (CL:FORMAT T "%"A space:  %" -> ~a~%%" (URL-ENCODE "A space:  "))
   (CL:FORMAT T "%"A unicode char: Ž%" -> ~a~%%" (URL-ENCODE "A unicode char: Ž"))
   (CL:FORMAT T "%"A path: /foo/bar%" -> ~a~%%" (URL-ENCODE "A path: /foo/bar"))
   (CL:FORMAT T "%"Empty string: %" -> ~a~%%" (URL-ENCODE "")))

(CL:DEFUN HTMLTEST.HTML-ENCODE ()
   "Test URL-ENCODE"
   (CL:FORMAT T "Testing url-encode:~%%")
   (CL:FORMAT T "%"Hello, world!%" -> %"~a%"~%%" (HTML-ENCODE "Hello, world!"))
   (CL:FORMAT T "%"1234567890%" -> %"~a%"~%%" (HTML-ENCODE "1234567890"))
   (CL:FORMAT T "%"Special chars: !@#$%%^&*()_+=-`~~[]{}|;':%",./<>?%" -> %"~a%"~%%" (HTML-ENCODE
                                                                                      
                                                                  "!@#$%%^&*()_+=-`~[]{}|;':%",./<>?"
                                                                                      ))
   (CL:FORMAT T "%"A space:  %" -> %"~a%"~%%" (HTML-ENCODE " "))
   (CL:FORMAT T "%"A unicode char: Ž%" -> %"~a%"~%%" (HTML-ENCODE "Ž"))
   (CL:FORMAT T "%"Some HTML: %"Do not end the </title> here%" -> %"~a%"~%%" (HTML-ENCODE 
                                                                       "Do not end the </title> here"
                                                                                    ))
   (CL:FORMAT T "%"Empty string: %" -> %"~a%"~%%" (HTML-ENCODE "")))



(* ;; "HCFILES for HTML")


(CL:DEFUN PARSE-DIRECTORIES (FILEPATH)
   "Return a list of the component directories in PATHNAME"
   [for POS from 1 until (IGREATERP POS (NCHARS FILEPATH))
      collect (LET ((END (STRPOS ">" FILEPATH POS)))
                   (COND
                      (END (PROG1 (SUBSTRING FILEPATH POS (IPLUS END -1))
                                  (SETQ POS END)))
                      (T (PROG1 (SUBSTRING FILEPATH POS)
                             (SETQ POS (IPLUS 1 (NCHARS FILEPATH))))])

(CL:DEFUN DOT-FILE-P (NAME)
   "Does the NAME portion of a file path start with a period?"
   (EQ (CHARCODE %.)
       (NTHCHARCODE NAME 1)))

(CL:DEFUN MAKE-CHILD-DIRECTORY (PARENT CHILD)
   (LET ((UNPACKED (UNPACKFILENAME.STRING PARENT)))
        (PACKFILENAME.STRING (APPEND (LIST 'DIRECTORY (CONCAT (LISTGET UNPACKED 'DIRECTORY)
                                                             ">" CHILD))
                                    UNPACKED))))

(DEFMACRO WITH-BACKTRACE (&BODY (BODY DECLS ENV))
   "If the BODY's execution signals any condition, show a backtrace and continue"
   `(CONDITIONS:HANDLER-CASE (PROGN ,@BODY)
           (CONDITION (E)
                  (CL:FORMAT T "!! Handling error ~s~%%" E)
                  (BACKTRACE NIL NIL 3))))

(CL:DEFUN HTML-FILES (&KEY (BASE (PSEUDOFILENAME (MEDLEYDIR)))
                           INCLUDE
                           (OUTPUT-DIR "{DSK}/tmp/html")
                           REDO SUBSETS (DRIBBLE T)
                           (PAUSE T))

   (* ;; "BASE is the root directory. Doesn't replace HTML files except when REDO")

   (* ;; "SUBSETS is some combination of TEDIT, HTML, PRETTY, and INDEX")

   (DECLARE (GLOBALVARS *COMMON-LISP-READ-ENVIRONMENT*))
   (LET*
    [(THIS.WINDOW (PROCESSPROP (THIS.PROCESS)
                         'WINDOW]
    (FILESLOAD SKETCH)
    (FONTSET 'STANDARD)
    (COND
       (DRIBBLE (DRIBBLE "{DSK}<tmp>html.log")))
    [COND
       ((NOT PAUSE)
        (WINDOWPROP THIS.WINDOW 'PAGEFULLFN #'NILL]
    (CL:UNWIND-PROTECT
        [CL:FLET
         ((CHILD (DIR)
                 (MAKE-CHILD-DIRECTORY BASE DIR)))
         (LET
          ((DIRLIST (LIST (OR (MAPCAR #'CHILD INCLUDE)
                              BASE)))
           [PHASES (OR SUBSETS '(TEDIT PRETTY INDEX HRULE]
           (OUTPUT-EXT ".html"))
          (WHILE DIRLIST
             DO
             (SETQ BASE (POP DIRLIST))
             (FOR SRCPATH IN (DIRECTORY (CONCAT BASE "*.*;"))
                DO (PROG* [(SRC (UNPACKFILENAME.STRING SRCPATH))
                           [NAME (U-CASE (LISTGET SRC 'NAME]
                           [EXT (U-CASE (LISTGET SRC 'EXTENSION]
                           (DIR (LISTGET SRC 'DIRECTORY))
                           (DIR-HIERARCHY (PARSE-DIRECTORIES DIR))
                           (DEEPEST-CHILD-DIR (CAR (LAST DIR-HIERARCHY)))
                           FRDY LDGP DEST (NOV (PACKFILENAME `(VERSION NIL ,@SRC]

                    (* ;; "any directory names, push them off and do them in another phase")

                          (COND
                             ((DIRECTORYNAMEP SRCPATH)
                              [COND
                                 ((DOT-FILE-P DEEPEST-CHILD-DIR)
                                  (CL:FORMAT T "Skipping dot-dir ~a~%%" SRCPATH))
                                 (T (CL:FORMAT T "Will process dir ~a later~%%" SRCPATH)
                                    (SETQ DIRLIST (NCONC1 DIRLIST SRCPATH]
                              (RETURN)))
                          (COND
                             ((DOT-FILE-P NAME)
                              (CL:FORMAT T "Skipping dot-file ~a~%%" SRCPATH)
                              (RETURN)))
                          (COND
                             ((MEMBER EXT
                                     '("PDF" "PS" "SKIP" "HTML" "LCOM" "DFASL" "SH" "SYSOUT" 
                                             "DRIBBLE" "IMPTR" "DISPLAYFONT" "ALL" "DATABASE" 
                                             "DOCKERIGNORE" "GITIGNORE"))

                              (* ;; "ignore any of these extensions")

                              (CL:FORMAT T "Ignoring ~a due to extension~%%" SRCPATH)
                              (RETURN)))
                          (SETQ DEST (PACKFILENAME.STRING 'DIRECTORY OUTPUT-DIR 'BODY
                                            (CONCAT NOV OUTPUT-EXT)))
                          (COND
                             ((AND (NOT REDO)
                                   (INFILEP DEST))
                              (CL:FORMAT T "~a already exists, skipping it~%%" DEST)
                              (RETURN)))
                          (IF (MEMB 'TEDIT PHASES)
                              THEN (IF [OR (MEMB EXT '(TEDIT TED SKETCH BRAVO))
                                           (CAR (NLSETQ (TEDIT.FORMATTEDFILEP SRCPATH]
                                       THEN (IF (EQ REDO 'TEST)
                                                THEN (CL:FORMAT T "Testing open ~a..." SRCPATH)
                                                     (CLOSEF? (OPENTEXTSTREAM SRCPATH))
                                              ELSE (CL:FORMAT T "Printing ~a to ~a~%%" SRCPATH DEST)
                                                   (CONVERT-TEDIT-TO-HTML SRCPATH DEST))
                                            (CL:FORMAT T "Done with TEdit file~%%"))
                            ELSE (LET (LSFP)
                                      (IF (AND (MEMB 'PRETTY PHASES)
                                               (MEMBER EXT '(NIL IL "IL" "" "LISP" LISP))
                                               [SETQ LSFP (CAR (NLSETQ (LISPSOURCEFILEP SRCPATH]
                                               (NEQ LSFP *COMMON-LISP-READ-ENVIRONMENT*))
                                          THEN (CL:FORMAT T "Converting source file ~a to ~a...~%%" 
                                                      SRCPATH DEST)
                                               (CONVERT-SOURCE-TO-HTML SRCPATH DEST)
                                               (CL:FORMAT T "Done with source file~%%"]
        (WINDOWPROP THIS.WINDOW 'PAGEFULLFN NIL)
        (DRIBBLE))))

(CL:DEFUN CONVERT-SOURCE-TO-HTML (SRC DEST)
   "Convert the Lisp source code at SRC to an HTML file at DEST, optionally logging storage used"
   (WITH-BACKTRACE (CL:WITH-OPEN-STREAM (STR (OPENIMAGESTREAM DEST))
                          (PRETTYFILEINDEX SRC NIL STR))))

(CL:DEFUN CONVERT-TEDIT-TO-HTML (SRC DEST)
   "Convert the TEdit file at SRC to an HTML file at DEST, optionally logging storage used"
   [WITH-BACKTRACE (CL:WITH-OPEN-STREAM (S (OPENTEXTSTREAM SRC))
                          (TEDIT.FORMAT.HARDCOPY S DEST T NIL NIL NIL 'HTML])



(* ;; "misc")


(CL:DEFUN WINDOWTEST.SHOW-TEXT-BOUNDS (&OPTIONAL REUSE-WINDOW)
   "Output text, draw bounds, dump widths"

   (* ;; "Remember: Y = 0 is at the bottom left; Y decreases as you go down the page.")

   (* ;; "Text is drawn with baseline at current Y position")

   (LET* ((WINDOW (OR REUSE-WINDOW (CREATEW (create REGION
                                                   LEFT _ 610
                                                   BOTTOM _ 10
                                                   WIDTH _ (HTML.INCHES-TO-POINTS 8.5)
                                                   HEIGHT _ (HTML.INCHES-TO-POINTS 11))
                                          "Text Bounds")))
          (STREAM (WINDOWPROP WINDOW 'DSP))
          (SAMPLE "All work and no play makes Jack a dull boy. 1234567890")
          (FONT10 (FONTCREATE 'HELVETICA 10 'REGULAR NIL STREAM))
          (FONT20 (FONTCREATE 'HELVETICA 18 'REGULAR NIL STREAM))
          STARTING-X STARTING-Y BASELINE-Y FONT-HEIGHT SAMPLE-WIDTH)
         (CLEARW WINDOW)

         (* ;; "display a string with a corner of a box at its origin, the width of the text")

         (DSPFONT FONT10 STREAM)
         (TERPRI STREAM)
         (SETQ FONT-HEIGHT (FONTPROP (DSPFONT NIL STREAM)
                                  'HEIGHT))
         (SETQ STARTING-X (DSPXPOSITION NIL STREAM))
         (SETQ STARTING-Y (DSPYPOSITION NIL STREAM))
         (SETQ SAMPLE-WIDTH (STRINGWIDTH SAMPLE (DSPFONT NIL STREAM)))
         (CL:FORMAT STREAM SAMPLE)
         (DRAWPOLYGON (LIST (CONS (+ STARTING-X SAMPLE-WIDTH)
                                  STARTING-Y)
                            (CONS STARTING-X STARTING-Y)
                            (CONS STARTING-X (+ STARTING-Y FONT-HEIGHT)))
                NIL NIL NIL STREAM)
         (TERPRI STREAM)
         (TERPRI STREAM)
         (TERPRI STREAM)))

(CL:DEFUN HTMLTEST.TRANSFORM (PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Move and scale a point"
   (CONS (+ X-TRANSLATE (ITIMES (CAR PT)
                               X-SCALE))
         (+ Y-TRANSLATE (ITIMES (CDR PT)
                               Y-SCALE))))

(CL:DEFUN HTMLTEST.TRANSFORM-POINTS (POINTS X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Translate and scale a list of points"
   (for PT in POINTS collect (HTMLTEST.TRANSFORM PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1863 2522 (GEN-TEST-BITMAP 1863 . 2522)) (2524 3339 (HTMLTEST.BITBLT 2524 . 3339)) (
3341 4337 (HTMLTEST.TALL-BITBLT 3341 . 4337)) (4339 5889 (HTMLTEST.BLTSHADE 4339 . 5889)) (5891 6609 (
HTMLTEST.ONE-PAGE-SIMPLE 5891 . 6609)) (6611 10175 (HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS 6611 . 
10175)) (10177 11014 (HTMLTEST.ONE-PAGE-WITH-CURVE 10177 . 11014)) (11016 11838 (
HTMLTEST.ONE-PAGE-DASHING 11016 . 11838)) (11840 13184 (HTMLTEST.ONE-PAGE-WITH-SPIRAL 11840 . 13184)) 
(13186 15350 (HTMLTEST.TEXT-TWO-FONTS 13186 . 15350)) (15352 15867 (HTMLTEST.TEXT-VERTICAL-OVERFLOW 
15352 . 15867)) (15869 16685 (HTMLTEST.TEXT-HORIZONTAL-OVERFLOW 15869 . 16685)) (16687 17437 (
HTMLTEST.SPECIAL-TEXT-ENTITIES 16687 . 17437)) (17439 17912 (HTMLTEST.BRIEF-TEXT 17439 . 17912)) (
17914 18655 (HTMLTEST.TABS-IN-TEXT 17914 . 18655)) (18657 20363 (HTMLTEST.BRUSHES 18657 . 20363)) (
20388 22808 (HTMLTEST.ONE-PAGE-WITH-COLOR 20388 . 22808)) (22836 28754 (HTMLTEST.FUNNY-CHARS 22836 . 
28754)) (28756 30334 (HTMLTEST.UNDEFINED-CHARS 28756 . 30334)) (30361 33793 (HTMLTEST.SHOW-TEXT-BOUNDS
 30361 . 33793)) (33795 38335 (HTMLTEST.SHOW-CHAR-BOUNDS 33795 . 38335)) (38337 43279 (
HTMLTEST.SHOW-COLOR-CHAR-BOUNDS 38337 . 43279)) (43304 44105 (HTMLTEST.NOTO-SANS 43304 . 44105)) (
44147 44975 (HTMLTEST.URL-ENCODE 44147 . 44975)) (44977 46093 (HTMLTEST.HTML-ENCODE 44977 . 46093)) (
46129 46637 (PARSE-DIRECTORIES 46129 . 46637)) (46639 46784 (DOT-FILE-P 46639 . 46784)) (46786 47101 (
MAKE-CHILD-DIRECTORY 46786 . 47101)) (47103 47411 (WITH-BACKTRACE 47103 . 47411)) (47413 52451 (
HTML-FILES 47413 . 52451)) (52453 52730 (CONVERT-SOURCE-TO-HTML 52453 . 52730)) (52732 53017 (
CONVERT-TEDIT-TO-HTML 52732 . 53017)) (53041 54904 (WINDOWTEST.SHOW-TEXT-BOUNDS 53041 . 54904)) (54906
 55177 (HTMLTEST.TRANSFORM 54906 . 55177)) (55179 55420 (HTMLTEST.TRANSFORM-POINTS 55179 . 55420)))))
STOP

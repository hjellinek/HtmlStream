(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "18-Oct-2024 15:14:54" {DSK}<Users>hjellinek>Projects>TrySVG>Lisp>HTMLSTREAMTEST.;16 5959   

      :CHANGES-TO (VARS HTMLSTREAMTESTCOMS)
                  (FUNCTIONS HTMLTEST.TEXT-TWO-FONTS HTMLTEST.ONE-PAGE-SIMPLE 
                         HTMLTEST.TEXT-VERTICAL-OVERFLOW HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS 
                         HTMLTEST.TRANSFORM-POINTS HTMLTEST.TRANSFORM)

      :PREVIOUS-DATE " 2-Oct-2024 16:20:28" 
{DSK}<Users>hjellinek>Projects>TrySVG>Lisp>HTMLSTREAMTEST.;1)


(PRETTYCOMPRINT HTMLSTREAMTESTCOMS)

(RPAQQ HTMLSTREAMTESTCOMS ((FUNCTIONS HTMLTEST.ONE-PAGE-SIMPLE HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS
                                  HTMLTEST.TEXT-TWO-FONTS HTMLTEST.TEXT-VERTICAL-OVERFLOW 
                                  HTMLTEST.TRANSFORM HTMLTEST.TRANSFORM-POINTS)))

(CL:DEFUN HTMLTEST.ONE-PAGE-SIMPLE (&OPTIONAL (FILE-NAME "/tmp/one-page-simple.html"))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (CL:FORMAT HTMLSTREAM "First line, Y position = ~d" (DSPYPOSITION NIL HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Second line, Y position = ~d" (DSPYPOSITION NIL HTMLSTREAM))
          (MOVETO 40 50 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Third line: Moved to 40, 50, Y position = ~d" (DSPYPOSITION NIL 
                                                                                      HTMLSTREAM))
          FILE-NAME))

(CL:DEFUN HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS (&OPTIONAL (FILE-NAME 
                                                                 "/tmp/one-page-simple-graphics.html"
                                                                   ))
   "Simple text + graphics test for a single-page document"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (CL:FORMAT HTMLSTREAM "First line!")
          (TERPRI HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Second line.")
          (MOVETO 40 50 HTMLSTREAM)
          (CL:FORMAT HTMLSTREAM "Third line: Moved to 40, 50.")
          (DRAWCIRCLE 140 150 130 NIL NIL HTMLSTREAM)
          (FILLCIRCLE 340 150 130 NIL HTMLSTREAM)
          (for ANGLE from 0 to 359 by 15 do (DRAWELLIPSE 200 250 100 180 ANGLE NIL NIL HTMLSTREAM))
          (for LINE-Y from 450 to 550 by 10 do (DRAWLINE 0 LINE-Y 800 LINE-Y (IQUOTIENT (- LINE-Y 449
                                                                                           )
                                                                                    10)
                                                      NIL HTMLSTREAM))
          (LET* ([POLY-POINTS '((100 . 100)
                                (50 . 125)
                                (150 . 175)
                                (200 . 100)
                                (150 . 50]
                 [STAR-POINTS '((50 . 0)
                                (21 . 90)
                                (98 . 35)
                                (2 . 35)
                                (79 . 90]
                 (TRANSFORMED-POLY-POINTS (HTMLTEST.TRANSFORM-POINTS POLY-POINTS -20 -20 3 3))
                 (TRANSFORMED-STAR-POINTS (HTMLTEST.TRANSFORM-POINTS STAR-POINTS 0 200 3 3)))
                (DRAWPOLYGON POLY-POINTS T '(ROUND 3)
                       '(4 2)
                       HTMLSTREAM)
                (DRAWPOLYGON TRANSFORMED-POLY-POINTS NIL '(ROUND 3)
                       '(4 2)
                       HTMLSTREAM)
                (FILLPOLYGON TRANSFORMED-STAR-POINTS NIL HTMLSTREAM)
                (LET [(CURVE-KNOTS '((10 . 10)
                                     (50 . 50)
                                     (100 . 10)
                                     (150 . 50]
                     (DRAWCURVE CURVE-KNOTS NIL NIL NIL HTMLSTREAM)))
          FILE-NAME))

(CL:DEFUN HTMLTEST.TEXT-TWO-FONTS (&OPTIONAL (FILE-NAME "/tmp/two-fonts.html"))
   "Output text, change fonts, output more text"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "Two fonts"))
          (LET ((FONT (FONTCREATE 'NOTO-SANS-DISPLAY 10 'REGULAR NIL HTMLSTREAM)))
               (CL:FORMAT HTMLSTREAM "This is in the default font, ~s.~%%" (DSPFONT NIL HTMLSTREAM))
               (TERPRI HTMLSTREAM)
               (DSPFONT FONT HTMLSTREAM)
               (CL:FORMAT HTMLSTREAM "We jst changed the font to ~s.~%%" (DSPFONT NIL HTMLSTREAM))
               (TERPRI HTMLSTREAM)
               FILE-NAME)))

(CL:DEFUN HTMLTEST.TEXT-VERTICAL-OVERFLOW (&OPTIONAL (FILE-NAME "/tmp/vertical-overflow.html"))
   "Output enough text to force a second page"
   (CL:WITH-OPEN-STREAM (HTMLSTREAM (OPENHTMLSTREAM FILE-NAME "One page test document"))
          (for LINE from 0 to 66 do (CL:FORMAT HTMLSTREAM "This is line ~d.  Y position = ~d" LINE
                                           (DSPYPOSITION NIL HTMLSTREAM))
                                    (TERPRI HTMLSTREAM))
          (CL:FORMAT HTMLSTREAM "This line will overflow.  Y position = ~d" (DSPYPOSITION NIL 
                                                                                   HTMLSTREAM))
          (TERPRI HTMLSTREAM)
          FILE-NAME))

(CL:DEFUN HTMLTEST.TRANSFORM (PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Move and scale a point"
   (CONS (+ X-TRANSLATE (ITIMES (CAR PT)
                               X-SCALE))
         (+ Y-TRANSLATE (ITIMES (CDR PT)
                               Y-SCALE))))

(CL:DEFUN HTMLTEST.TRANSFORM-POINTS (POINTS X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)
   "Translate and scale a list of points"
   (for PT in POINTS collect (HTMLTEST.TRANSFORM PT X-TRANSLATE Y-TRANSLATE X-SCALE Y-SCALE)))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (904 1615 (HTMLTEST.ONE-PAGE-SIMPLE 904 . 1615)) (1617 4062 (
HTMLTEST.ONE-PAGE-WITH-SIMPLE-GRAPHICS 1617 . 4062)) (4064 4694 (HTMLTEST.TEXT-TWO-FONTS 4064 . 4694))
 (4696 5420 (HTMLTEST.TEXT-VERTICAL-OVERFLOW 4696 . 5420)) (5422 5693 (HTMLTEST.TRANSFORM 5422 . 5693)
) (5695 5936 (HTMLTEST.TRANSFORM-POINTS 5695 . 5936)))))
STOP
